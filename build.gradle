plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.2.50'
    id "org.jetbrains.intellij" version "0.3.4"
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }

    apply plugin: 'java'
    apply plugin: 'org.jetbrains.intellij'

    intellij {
        version = '182.3684.40'
        type = 'IU'
        downloadSources = true
        plugins += ['DatabaseTools', 'Scala']
    }
}

sourceSets {
    main.kotlin.srcDirs = []
    main.java.srcDirs = []
    main.resources.srcDirs = []

    test.kotlin.srcDirs = []
    test.java.srcDirs = []
    test.resources.srcDirs = []
}


subprojects {
    apply plugin: 'kotlin'


    ext.kotlin_version = '1.2.50'
    sourceCompatibility = 1.8


    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    def srcDirs = ['src']
    def resourcesDirs = ["resources"]
    def testScrDirs = ['test/src']
    def testDataDirs = ['test/resources']

    sourceSets {
        main.kotlin.srcDirs = srcDirs
        main.java.srcDirs = srcDirs
        main.resources.srcDirs = resourcesDirs

        test.kotlin.srcDirs = testScrDirs
        test.java.srcDirs = testScrDirs
        test.resources.srcDirs = testDataDirs
    }
}


dependencies {
    compileOnly project(":zeppelin")
}

intellij {
    plugins project(':zeppelin')
}

group 'com.intellij'
version '1.0-SNAPSHOT'

task runIdea {
    dependsOn(assemble)
    dependsOn(runIde)
}

task copySandboxPlugins {
    childProjects.forEach { projectName, subproject ->
        copy {
            from "${subproject.buildDir}/idea-sandbox/plugins/"
            into "${rootProject.buildDir}/idea-sandbox/plugins/"
        }
    }
}